pkgbase=python
pkgname=($pkgbase{,-dev})
pkgver=3.10.2
pkgrel=1
pkgdesc='Interpreted, interactive, object-oriented programming language'
arch=(aarch64 x86_64 universal)
url='https://www.python.org'
license=(PSF)
options=(!staticlibs strip)

depends=(xz)
makedepends=(xz{,-dev})

source=("https://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.xz"
        'configure.patch'
        'no-pyc.patch')

md5sums=('SKIP' 'SKIP' SKIP)

_source_src_subdir="${pkgbase^}-$pkgver"

prepare() {
  cd "$srcdir/$_source_src_subdir"
  apply_patches
}

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  export \
    CARCH=$CARCH \
    MACHDEP=darwin \
    CPPFLAGS="$CPPFLAGS -I$MAKEDEPENDS/include" \
    LDFLAGS="$LDFLAGS -L$MAKEDEPENDS/lib"

  "../$_source_src_subdir/configure" \
    ac_sys_system=Darwin \
    ac_sys_release=$MACOSX_DEPLOYMENT_TARGET \
    ac_cv_file__dev_ptmx=no \
    ac_cv_file__dev_ptc=no \
    ac_cv_enable_implicit_function_declaration_error=no \
    --host=${CHOST} \
    --build=${MACHTYPE} \
    --prefix=/ \
    --enable-optimizations \
    --enable-shared \
    --without-lto \
    --with-computed-gotos \
    --without-ensurepip

  DESTDIR=/ \
  DISTUTILS_DEBUG=True \
  install_for_package install prefix
}

package_application_with_dev
