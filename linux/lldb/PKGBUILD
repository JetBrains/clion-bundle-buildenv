# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=lldb
pkgver=5.0.0
pkgrel=6
pkgdesc="Next generation, high-performance debugger"
arch=(i686 x86_64)
url="http://lldb.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
# makedepends=('cmake' 'libffi' 'python2' "ocaml=$_ocaml_ver" 'python-sphinx'
#              'ocaml-ctypes' 'ocaml-findlib' 'libedit' 'swig')
# depends=('llvm-libs' 'libxml2' 'python2' 'python2-six')
# depends=('gcc-libs' 'zlib' 'libffi' 'libedit' 'ncurses')
makedepends=('cmake' 'python' 'swig')
depends=(zlib python)
options=(!staticlibs !debug)
source=(https://releases.llvm.org/${pkgver}/llvm-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/cfe-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/clang-tools-extra-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/compiler-rt-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/lld-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/lldb-${pkgver}.src.tar.xz
        0500-disable-lldb-soversion.patch
        0501-Don-t-build-lldb-mi-and-intel-mpx.patch
        0502-Allow-Python-to-find-its-home-when-relocated.patch
        0503-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch)
sha256sums=('e35dcbae6084adcf4abb32514127c5eabd7d63b733852ccdb31e06f1373136da'
            '019f23c2192df793ac746595e94a403908749f8e0c484b403476d2611dd20970'
            'c0a0ca32105e9881d86b7ca886220147e686edc97fdb9f3657c6659dc6568b7d'
            'f34a8d4e12b9dbfc0d8bd9954b26c70fd78333000ecbbeb9d3a034f4e4915ce6'
            '714a766e784153cf7f9774d373ec5a2ad4b323a69c21aefadb3acf2ce1e1d859'
            '2782c5bd9086effbbc17ae11519272202dd51e9ec9565114ebf6521b06e50c9e'
            'e2dee38cfdd6cee22dc3af1e79fd62ea0e4623732de0cf340065dd722734bd94')

prepare() {
  cd "$srcdir/llvm-${pkgver}.src"
  mkdir build

  mv "$srcdir/cfe-${pkgver}.src" tools/clang
  mv "$srcdir/lldb-${pkgver}.src" tools/lldb

  cd tools/lldb
  patch -p1 -i "${srcdir}/0500-disable-lldb-soversion.patch"
  patch -p1 -i "${srcdir}/0501-Don-t-build-lldb-mi-and-intel-mpx.patch"
  patch -p1 -i "${srcdir}/0502-Allow-Python-to-find-its-home-when-relocated.patch"
  patch -p1 -i "${srcdir}/0503-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch"

  # # https://bugs.llvm.org/show_bug.cgi?id=34123
  # sed -i '/LLVMSupport/d' tools/clang/tools/extra/clangd/tool/CMakeLists.txt
}

_llvm_pkgversion() {
  echo "${pkgname} (LLVM${PKGVERSION:+; ${PKGVERSION}}) ${pkgver}"
}

_cmake_escape_semicolon() {
  sed 's/;/\\;/g'
}

build() {
  cd "$srcdir/llvm-${pkgver}.src/build"

  CFLAGS="${CPPFLAGS} ${CFLAGS}" \
  CXXFLAGS="${CPPFLAGS} ${CXXFLAGS}" \
  cmake \
    -DCMAKE_VERBOSE_MAKEFILE=TRUE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${PREFIX} \
    -DPYTHON_EXECUTABLE=${PREFIX}/bin/python \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    -DLLDB_DISABLE_LIBEDIT=TRUE \
    -DLLDB_DISABLE_CURSES=TRUE \
    -DLLDB_VERSION_STRING="$(_llvm_pkgversion | _cmake_escape_semicolon)" \
    ..

  make

  # # Disable automatic installation of components that go into subpackages
  # sed -i '/\(clang\|lld\|lldb\)\/cmake_install.cmake/d' tools/cmake_install.cmake
  # sed -i '/extra\/cmake_install.cmake/d' tools/clang/tools/cmake_install.cmake
  # sed -i '/compiler-rt\/cmake_install.cmake/d' projects/cmake_install.cmake
}

check() {
  cd "$srcdir/llvm-${pkgver}.src/build"
  make check-{llvm,clang}
}

package() {
  cd "$srcdir/llvm-${pkgver}.src"

  make -C build/tools/lldb DESTDIR="${pkgdir}" install

  # This resolves to something like ${PREFIX}/lib/python3.6/site-packages
  local python_site_packages=$(${PREFIX}/bin/python -c \
    'from distutils import sysconfig; print(sysconfig.get_python_lib(True, False));')

  # TeamCity doesn't handle symlinks in artifact dependencies;
  # replace symlink with empty shared object depending.
  local cc=${CC:-cc}
  $cc -nostdlib -shared -fPIC \
    -Wl,-rpath='$ORIGIN/../../..' \
    -Wl,--no-as-needed -L${pkgdir}${PREFIX}/lib -llldb \
    -o "${pkgdir}${python_site_packages}/lldb/_lldb.so"

  # No one knows what the lldb-argdumper symlink in python site-packages is for anyway
  # http://lists.llvm.org/pipermail/lldb-dev/2015-February/006625.html
  rm "${pkgdir}${python_site_packages}/lldb/lldb-argdumper"

  # # https://bugs.archlinux.org/task/50759
  # sed -i "/import_module('_lldb')/s/_lldb/lldb.&/" \
  #   "${pkgdir}${python_site_packages}/lldb/__init__.py"

  # # Remove bundled six library
  # rm "${pkgdir}${python_site_packages}/six.py"

  # # Compile Python scripts
  # python2 -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"
  # python2 -O -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"

  install -Dm644 LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/llvm/LICENSE"
  install -Dm644 tools/${pkgname}/LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
