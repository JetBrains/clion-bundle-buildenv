# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=lldb
pkgver=9.0.0rc6
pkgrel=1
pkgdesc="Next generation, high-performance debugger"
arch=(i686 x86_64)
url="http://lldb.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
# makedepends=('cmake' 'libffi' 'python2' "ocaml=$_ocaml_ver" 'python-sphinx'
#              'ocaml-ctypes' 'ocaml-findlib' 'libedit' 'swig')
# depends=('llvm-libs' 'libxml2' 'python2' 'python2-six')
# depends=('gcc-libs' 'zlib' 'libffi' 'libedit' 'ncurses')
makedepends=('python' 'swig')
depends=(zlib python)
options=(!staticlibs !debug)

if [[ $pkgver != *rc* ]]; then
source=(https://releases.llvm.org/${pkgver}/llvm-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/cfe-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/lldb-${pkgver}.src.tar.xz)
else
source=(https://prereleases.llvm.org/${pkgver/rc//rc}/llvm-${pkgver}.src.tar.xz
        https://prereleases.llvm.org/${pkgver/rc//rc}/cfe-${pkgver}.src.tar.xz
        https://prereleases.llvm.org/${pkgver/rc//rc}/lldb-${pkgver}.src.tar.xz)
fi
source+=(0001-build-Disable-SOVERSION-for-liblldb.so.patch
         0002-Allow-Python-to-find-its-home-when-relocated.patch
         0003-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch
         0004-Support-LLDB_VERSION_STRING-in-reported-version.patch)

sha256sums=('8d7f9db854c1d078387e9a707c57d69ad66c7a27b47d785fde6e105b8cd802c1'
            'be920682bf410e808a42f80b36f366c12f8a137b642a67fc50665af56756699c'
            '3827c5ab26ff5120ca296a51f0a039784ba9fa3662ac6f3d52ebe181664c1ef9'
            '024d10a7fb32a5992e59349ca0fe21340f9641aab17c460f708556d3bc32bc97'
            '5c9fbc5fd1b62d5961fdc4ad2a919b9c886545345c0d92cbbde783910399617d'
            'c8d6fa829c7d2971d6042658c55fce5e1eb51fc8b85dcb71f13aca37289008a6'
            '85d165018a379efbda8e94133c84177c317b870ed4c6018bc30261e32f238d80')

prepare() {
  cd "$srcdir/llvm-${pkgver}.src"
  mkdir -p build

  mv "$srcdir/cfe-${pkgver}.src" tools/clang
  mv "$srcdir/lldb-${pkgver}.src" tools/lldb

  cd tools/lldb
  patch -p1 -i "${srcdir}/0001-build-Disable-SOVERSION-for-liblldb.so.patch"
  patch -p1 -i "${srcdir}/0002-Allow-Python-to-find-its-home-when-relocated.patch"
  patch -p1 -i "${srcdir}/0003-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch"
  patch -p1 -i "${srcdir}/0004-Support-LLDB_VERSION_STRING-in-reported-version.patch"
}

_llvm_pkgversion() {
  echo "${pkgname} (LLVM${PKGVERSION:+; ${PKGVERSION}}) ${pkgver}"
}

_cmake_escape_semicolon() {
  sed 's/;/\\;/g'
}

build() {
  cd "$srcdir/llvm-${pkgver}.src/build"

  CFLAGS="${CPPFLAGS} ${CFLAGS}" \
  CXXFLAGS="${CPPFLAGS} ${CXXFLAGS}" \
  cmake \
    -DCMAKE_VERBOSE_MAKEFILE=TRUE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${PREFIX} \
    -DPYTHON_EXECUTABLE=${PREFIX}/bin/python \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    -DLLDB_DISABLE_LIBEDIT=TRUE \
    -DLLDB_DISABLE_CURSES=TRUE \
    -DLLDB_VERSION_STRING="$(_llvm_pkgversion | _cmake_escape_semicolon)" \
    ..

  make

  # # Disable automatic installation of components that go into subpackages
  # sed -i '/\(clang\|lld\|lldb\)\/cmake_install.cmake/d' tools/cmake_install.cmake
  # sed -i '/extra\/cmake_install.cmake/d' tools/clang/tools/cmake_install.cmake
  # sed -i '/compiler-rt\/cmake_install.cmake/d' projects/cmake_install.cmake
}

check() {
  cd "$srcdir/llvm-${pkgver}.src/build"
  make check-{llvm,clang}
}

package() {
  cd "$srcdir/llvm-${pkgver}.src"

  make -C build/tools/lldb DESTDIR="${pkgdir}" install

  # This resolves to something like ${PREFIX}/lib/python3.6/site-packages
  local python_site_packages=$(${PREFIX}/bin/python -c \
    'from distutils import sysconfig; print(sysconfig.get_python_lib(True, False));')

  # TeamCity doesn't handle symlinks in artifact dependencies;
  # replace symlink with empty shared object depending.
  local cc=${CC:-cc}
  $cc -nostdlib -shared -fPIC \
    -Wl,-rpath='$ORIGIN/../../..' \
    -Wl,--no-as-needed -L${pkgdir}${PREFIX}/lib -llldb \
    -o "${pkgdir}${python_site_packages}/lldb/_lldb.so"

  # No one knows what the lldb-argdumper symlink in python site-packages is for anyway
  # http://lists.llvm.org/pipermail/lldb-dev/2015-February/006625.html
  rm "${pkgdir}${python_site_packages}/lldb/lldb-argdumper"

  # LLDB 9
  rm -f "${pkgdir}${PREFIX}/bin/lldb-instr"
  rm -f "${pkgdir}${PREFIX}/bin/lldb-mi"
  rm -f "${pkgdir}${PREFIX}/bin/lldb-vscode"

  # # https://bugs.archlinux.org/task/50759
  # sed -i "/import_module('_lldb')/s/_lldb/lldb.&/" \
  #   "${pkgdir}${python_site_packages}/lldb/__init__.py"

  # # Remove bundled six library
  # rm "${pkgdir}${python_site_packages}/six.py"

  # # Compile Python scripts
  # python2 -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"
  # python2 -O -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"

  install -Dm644 LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/llvm/LICENSE"
  install -Dm644 tools/${pkgname}/LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
