# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=lldb
pkgver=9.0.0
pkgrel=1
pkgdesc="Next generation, high-performance debugger"
arch=(i686 x86_64)
url="http://lldb.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
# makedepends=('cmake' 'libffi' 'python2' "ocaml=$_ocaml_ver" 'python-sphinx'
#              'ocaml-ctypes' 'ocaml-findlib' 'libedit' 'swig')
# depends=('llvm-libs' 'libxml2' 'python2' 'python2-six')
# depends=('gcc-libs' 'zlib' 'libffi' 'libedit' 'ncurses')
makedepends=('python' 'swig')
depends=(zlib python)
options=(!staticlibs !debug)

if [[ $pkgver != *rc* ]]; then
source=(https://releases.llvm.org/${pkgver}/llvm-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/cfe-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/lldb-${pkgver}.src.tar.xz)
else
source=(https://prereleases.llvm.org/${pkgver/rc//rc}/llvm-${pkgver}.src.tar.xz
        https://prereleases.llvm.org/${pkgver/rc//rc}/cfe-${pkgver}.src.tar.xz
        https://prereleases.llvm.org/${pkgver/rc//rc}/lldb-${pkgver}.src.tar.xz)
fi
source+=(0001-Support-LLVM_APPEND_VC_REV-option-in-clang.patch
         0001-build-Disable-SOVERSION-for-liblldb.so.patch
         0002-Allow-Python-to-find-its-home-when-relocated.patch
         0003-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch
         0004-Support-LLDB_VERSION_STRING-in-reported-version.patch)

sha256sums=('d6a0565cf21f22e9b4353b2eb92622e8365000a9e90a16b09b56f8157eabfe84'
            '7ba81eef7c22ca5da688fdf9d88c20934d2d6b40bfe150ffd338900890aa4610'
            '1e4c2f6a1f153f4b8afa2470d2e99dab493034c1ba8b7ffbbd7600de016d0794'
            'd6bc8b7e1b9a1d9a857c6c2670f9bdf59f9c4a3e1121a61c433e5b01729b519b'
            '024d10a7fb32a5992e59349ca0fe21340f9641aab17c460f708556d3bc32bc97'
            '5c9fbc5fd1b62d5961fdc4ad2a919b9c886545345c0d92cbbde783910399617d'
            'c8d6fa829c7d2971d6042658c55fce5e1eb51fc8b85dcb71f13aca37289008a6'
            'c0985e0e99059b64bfdfa49cbae4ceb4b0508e99e5586bd191572f7896022173')

prepare() {
  ln -s "$srcdir/llvm-${pkgver}.src" "$srcdir/llvm"
  ln -s "$srcdir/cfe-${pkgver}.src" "$srcdir/clang"
  ln -s "$srcdir/lldb-${pkgver}.src" "$srcdir/lldb"

  cd "$srcdir/clang"
  patch -p1 -i "${srcdir}/0001-Support-LLVM_APPEND_VC_REV-option-in-clang.patch"

  cd "$srcdir/lldb"
  patch -p1 -i "${srcdir}/0001-build-Disable-SOVERSION-for-liblldb.so.patch"
  patch -p1 -i "${srcdir}/0002-Allow-Python-to-find-its-home-when-relocated.patch"
  patch -p1 -i "${srcdir}/0003-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch"
  patch -p1 -i "${srcdir}/0004-Support-LLDB_VERSION_STRING-in-reported-version.patch"
}

_llvm_pkgversion() {
  echo "LLVM${PKGVERSION:+; ${PKGVERSION}}"
}

_cmake_escape_semicolon() {
  sed 's/;/\\;/g'
}

build() {
  mkdir -p "${srcdir}"/build-${CHOST}
  cd "${srcdir}"/build-${CHOST}

  CFLAGS="${CPPFLAGS} ${CFLAGS}" \
  CXXFLAGS="${CPPFLAGS} ${CXXFLAGS}" \
  cmake \
    -DCMAKE_VERBOSE_MAKEFILE=TRUE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${PREFIX} \
    -DPYTHON_EXECUTABLE=${PREFIX}/bin/python \
    -DLLVM_ENABLE_PROJECTS="clang;lldb" \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    -DLLDB_DISABLE_LIBEDIT=TRUE \
    -DLLDB_DISABLE_CURSES=TRUE \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DLLVM_INCLUDE_BENCHMARKS=OFF \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DLLVM_APPEND_VC_REV=OFF \
    -DLLDB_VERSION_STRING="$(_llvm_pkgversion | _cmake_escape_semicolon)" \
    "$srcdir/llvm"

  make

  # # Disable automatic installation of components that go into subpackages
  # sed -i '/\(clang\|lld\|lldb\)\/cmake_install.cmake/d' tools/cmake_install.cmake
  # sed -i '/extra\/cmake_install.cmake/d' tools/clang/tools/cmake_install.cmake
  # sed -i '/compiler-rt\/cmake_install.cmake/d' projects/cmake_install.cmake
}

check() {
  cd "${srcdir}"/build-${CHOST}
  make check-{llvm,clang}
}

package() {
  cd "${srcdir}"/build-${CHOST}

  make -C "${srcdir}"/build-${CHOST}/tools/lldb DESTDIR="${pkgdir}" install

  # This resolves to something like ${PREFIX}/lib/python3.6/site-packages
  local python_site_packages=$(${PREFIX}/bin/python -c \
    'from distutils import sysconfig; print(sysconfig.get_python_lib(True, False));')

  # TeamCity doesn't handle symlinks in artifact dependencies;
  # replace symlink with empty shared object depending.
  local cc=${CC:-cc}
  $cc -nostdlib -shared -fPIC \
    -Wl,-rpath='$ORIGIN/../../..' \
    -Wl,--no-as-needed -L${pkgdir}${PREFIX}/lib -llldb \
    -o "${pkgdir}${python_site_packages}/lldb/_lldb.so"

  # No one knows what the lldb-argdumper symlink in python site-packages is for anyway
  # http://lists.llvm.org/pipermail/lldb-dev/2015-February/006625.html
  rm "${pkgdir}${python_site_packages}/lldb/lldb-argdumper"

  # LLDB 9
  rm -f "${pkgdir}${PREFIX}/bin/lldb-instr"
  rm -f "${pkgdir}${PREFIX}/bin/lldb-mi"
  rm -f "${pkgdir}${PREFIX}/bin/lldb-vscode"

  # # https://bugs.archlinux.org/task/50759
  # sed -i "/import_module('_lldb')/s/_lldb/lldb.&/" \
  #   "${pkgdir}${python_site_packages}/lldb/__init__.py"

  # # Remove bundled six library
  # rm "${pkgdir}${python_site_packages}/six.py"

  # # Compile Python scripts
  # python2 -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"
  # python2 -O -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"

  install -Dm644 "${srcdir}"/llvm/LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/llvm/LICENSE"
  install -Dm644 "${srcdir}"/lldb/LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
