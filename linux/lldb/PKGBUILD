# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=lldb
pkgver=7.0.1
pkgrel=1
pkgdesc="Next generation, high-performance debugger"
arch=(i686 x86_64)
url="http://lldb.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
# makedepends=('cmake' 'libffi' 'python2' "ocaml=$_ocaml_ver" 'python-sphinx'
#              'ocaml-ctypes' 'ocaml-findlib' 'libedit' 'swig')
# depends=('llvm-libs' 'libxml2' 'python2' 'python2-six')
# depends=('gcc-libs' 'zlib' 'libffi' 'libedit' 'ncurses')
makedepends=('python' 'swig')
depends=(zlib python)
options=(!staticlibs !debug)
source=(https://releases.llvm.org/${pkgver}/llvm-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/cfe-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/clang-tools-extra-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/compiler-rt-${pkgver}.src.tar.xz
        # https://releases.llvm.org/${pkgver}/lld-${pkgver}.src.tar.xz
        https://releases.llvm.org/${pkgver}/lldb-${pkgver}.src.tar.xz
        0001-build-Disable-SOVERSION-for-liblldb.so.patch
        0002-Don-t-build-lldb-mi-and-intel-features.patch
        0003-Allow-Python-to-find-its-home-when-relocated.patch
        0004-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch)
sha256sums=('a38dfc4db47102ec79dcc2aa61e93722c5f6f06f0a961073bd84b78fb949419b'
            'a45b62dde5d7d5fdcdfa876b0af92f164d434b06e9e89b5d0b1cbc65dfe3f418'
            '76b46be75b412a3d22f0d26279306ae7e274fe4d7988a2184c529c38a6a76982'
            '28af6e6f5d38a16538faddcb94f74d1837df3eac59c7de4107b84d583aadbf70'
            '1885f6a081d44fd994a3235620a04d0f18c38e7b02564dc65bb3b9b019f18bf9'
            '2982e4dcb792e1868d671429c32d61782339d94f509c89969119d55a59b72383'
            '83d55fe06babded11a739354921b8325de55b31ff52d43bb13294e1fd9717cbd')

prepare() {
  cd "$srcdir/llvm-${pkgver}.src"
  mkdir build

  mv "$srcdir/cfe-${pkgver}.src" tools/clang
  mv "$srcdir/lldb-${pkgver}.src" tools/lldb

  cd tools/lldb
  patch -p1 -i "${srcdir}/0001-build-Disable-SOVERSION-for-liblldb.so.patch"
  patch -p1 -i "${srcdir}/0002-Don-t-build-lldb-mi-and-intel-features.patch"
  patch -p1 -i "${srcdir}/0003-Allow-Python-to-find-its-home-when-relocated.patch"
  patch -p1 -i "${srcdir}/0004-python-Make-compatible-with-Python-3-w.r.t.-integer-.patch"

  # # https://bugs.llvm.org/show_bug.cgi?id=34123
  # sed -i '/LLVMSupport/d' tools/clang/tools/extra/clangd/tool/CMakeLists.txt
}

_llvm_pkgversion() {
  echo "${pkgname} (LLVM${PKGVERSION:+; ${PKGVERSION}}) ${pkgver}"
}

_cmake_escape_semicolon() {
  sed 's/;/\\;/g'
}

build() {
  cd "$srcdir/llvm-${pkgver}.src/build"

  CFLAGS="${CPPFLAGS} ${CFLAGS}" \
  CXXFLAGS="${CPPFLAGS} ${CXXFLAGS}" \
  cmake \
    -DCMAKE_VERBOSE_MAKEFILE=TRUE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${PREFIX} \
    -DPYTHON_EXECUTABLE=${PREFIX}/bin/python \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    -DLLDB_DISABLE_LIBEDIT=TRUE \
    -DLLDB_DISABLE_CURSES=TRUE \
    -DLLDB_VERSION_STRING="$(_llvm_pkgversion | _cmake_escape_semicolon)" \
    ..

  make

  # # Disable automatic installation of components that go into subpackages
  # sed -i '/\(clang\|lld\|lldb\)\/cmake_install.cmake/d' tools/cmake_install.cmake
  # sed -i '/extra\/cmake_install.cmake/d' tools/clang/tools/cmake_install.cmake
  # sed -i '/compiler-rt\/cmake_install.cmake/d' projects/cmake_install.cmake
}

check() {
  cd "$srcdir/llvm-${pkgver}.src/build"
  make check-{llvm,clang}
}

package() {
  cd "$srcdir/llvm-${pkgver}.src"

  make -C build/tools/lldb DESTDIR="${pkgdir}" install

  # This resolves to something like ${PREFIX}/lib/python3.6/site-packages
  local python_site_packages=$(${PREFIX}/bin/python -c \
    'from distutils import sysconfig; print(sysconfig.get_python_lib(True, False));')

  # TeamCity doesn't handle symlinks in artifact dependencies;
  # replace symlink with empty shared object depending.
  local cc=${CC:-cc}
  $cc -nostdlib -shared -fPIC \
    -Wl,-rpath='$ORIGIN/../../..' \
    -Wl,--no-as-needed -L${pkgdir}${PREFIX}/lib -llldb \
    -o "${pkgdir}${python_site_packages}/lldb/_lldb.so"

  # No one knows what the lldb-argdumper symlink in python site-packages is for anyway
  # http://lists.llvm.org/pipermail/lldb-dev/2015-February/006625.html
  rm "${pkgdir}${python_site_packages}/lldb/lldb-argdumper"

  # this is introduced in LLDB 6
  rm "${pkgdir}${PREFIX}/bin/lldb-test"

  # # https://bugs.archlinux.org/task/50759
  # sed -i "/import_module('_lldb')/s/_lldb/lldb.&/" \
  #   "${pkgdir}${python_site_packages}/lldb/__init__.py"

  # # Remove bundled six library
  # rm "${pkgdir}${python_site_packages}/six.py"

  # # Compile Python scripts
  # python2 -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"
  # python2 -O -m compileall "${pkgdir}${PREFIX}/lib/python2.7/site-packages/lldb"

  install -Dm644 LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/llvm/LICENSE"
  install -Dm644 tools/${pkgname}/LICENSE.TXT "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
