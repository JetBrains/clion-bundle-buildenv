# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.12.9
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="Next generation of the python high-level scripting language"
arch=(aarch64 i686 x86_64)
license=('custom')
url="http://www.python.org/"
depends=(expat zlib xz)
# depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
# makedepends=('tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal' 'llvm' 'gdb' 'xorg-server-xvfb')
# optdepends=('python-setuptools'
#             'python-pip'
#             'sqlite'
#             'mpdecimal: for decimal'
#             'xz: for lzma'
#             'tk: for tkinter')
# provides=('python3')
# replaces=('python3')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
        '0001-python-config-Ignore-first-argument-if-it-s-a-.py-sc.patch'
        '0002-Suppress-writing-bytecode-files-by-default.patch'
        '0003-make-install-Don-t-compile-__pycache__.patch'
        '0004-python-packages-hints.patch'
        '0005-python-config-libpython.patch')
md5sums=('880942124f7d5c01e7b65cbad62dc873'
         '5c364c89632fd5080f6ec067d1df886e'
         'ae5fc24e9afa693f967a6f4687f662e8'
         '14b0bd9e47a0976374927f08c65b93b4'
         '130f5a0fad69b467dd3388c7f7050d1b'
         '141b45d34827b976a851809c21482732')

prepare() {
  cd Python-${pkgver}

  # GDB configure quirk
  patch -p1 -i ../0001-python-config-Ignore-first-argument-if-it-s-a-.py-sc.patch

  # Don't pollute relocatable standard library with *.pyc and __pycache__
  patch -p1 -i ../0002-Suppress-writing-bytecode-files-by-default.patch

  # Don't do unnecessary job
  patch -p1 -i ../0003-make-install-Don-t-compile-__pycache__.patch

  # Help python to find bundled packages
  patch -p1 -i ../0004-python-packages-hints.patch

  # add libpython back to python-config
  patch -p1 -i ../0005-python-config-libpython.patch

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # # rather than copies shipped in the tarball
  # rm -r Modules/expat
  # rm -r Modules/zlib
  # rm -r Modules/_ctypes/{darwin,libffi}*
  # rm -r Modules/_decimal/libmpdec
}

build() {
  cd Python-${pkgver}

  # Disable bundled pip & setuptools
  ./configure --prefix=${PREFIX} \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --with-system-expat \
              --without-ensurepip

  # Python Makefile uses both configure and environmental variables,
  # which results in duplicated flags.
  env --unset={CPPFLAGS,CFLAGS,CXXFLAGS,LDFLAGS} make
}

check() {
  cd Python-${pkgver}

  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
  LC_CTYPE=en_US.UTF-8 \
    "${srcdir}/Python-${pkgver}/python" -m test.regrtest -v -uall
}

package() {
  cd Python-${pkgver}

  # Hack to avoid building again
  sed -i 's/^all:.*$/all: build_all/' Makefile

  # `make install` tends to ru python commands with LD_LIBRARY_PATH overridden;
  # resolve libfakeroot.so to its absolute name, otherwise it won't be found.
  local ld_preload=$(IFS=':'; for dir in $LD_LIBRARY_PATH; do
      test -d "$dir" &&
        find "$dir" -maxdepth 1 -name "$LD_PRELOAD" -type f -exec readlink -e {} \;
    done | head -1)
  LD_PRELOAD="$ld_preload" make DESTDIR="${pkgdir}" install

  LD_PRELOAD="$ld_preload" LD_LIBRARY_PATH=$(pwd) ./python -E \
    -c 'import shutil, sys; shutil.make_archive(sys.argv[1], "zip", sys.argv[2])' \
            "${pkgdir}"${PREFIX}/lib/python${_pybasever//.} "$(pwd)"/Lib
  chmod 644 "${pkgdir}"${PREFIX}/lib/python${_pybasever//.}.zip

  # create os.pyc
  LD_PRELOAD="$ld_preload" LD_LIBRARY_PATH=$(pwd) ./python -E \
    -Wi "${pkgdir}"${PREFIX}/lib/python${_pybasever}/compileall.py \
      -d ${PREFIX}/lib/python${_pybasever} -f -b \
      "${pkgdir}"${PREFIX}/lib/python${_pybasever}/os.py \
      "${pkgdir}"${PREFIX}/lib/python${_pybasever}/_sysconfigdata*.py

  mv -T "${pkgdir}"${PREFIX}/lib/python${_pybasever} \
        "${pkgdir}"${PREFIX}/lib/python${_pybasever}.tmp
  mkdir "${pkgdir}"${PREFIX}/lib/python${_pybasever}
  mv -t "${pkgdir}"${PREFIX}/lib/python${_pybasever} \
        "${pkgdir}"${PREFIX}/lib/python${_pybasever}.tmp/{lib-dynload,site-packages,os.pyc,_sysconfigdata*.pyc}
  rm -r "${pkgdir}"${PREFIX}/lib/python${_pybasever}.tmp

  # Why are these not done by default...
  ln -s python3               "${pkgdir}"${PREFIX}/bin/python
  ln -s python3-config        "${pkgdir}"${PREFIX}/bin/python-config
  # ln -s idle3                 "${pkgdir}"${PREFIX}/bin/idle
  # ln -s pydoc3                "${pkgdir}"${PREFIX}/bin/pydoc
  # ln -s python${_pybasever}.1 "${pkgdir}"${PREFIX}/share/man/man1/python.1

  # # some useful "stuff" FS#46146
  # install -dm755 "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/{i18n,scripts}
  # install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/i18n/
  # install -m755 Tools/scripts/{README,*py} "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/scripts/

  # License
  install -Dm644 LICENSE "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
