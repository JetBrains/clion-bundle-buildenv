# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from Arch Linux packaging project.
#
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=${PKG_VERSION[$pkgname]:-3.6.2}
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="Next generation of the python high-level scripting language"
arch=(i686 x86_64)
license=('custom')
url="http://www.python.org/"
# depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
# makedepends=('tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal' 'llvm' 'gdb' 'xorg-server-xvfb')
# optdepends=('python-setuptools'
#             'python-pip'
#             'sqlite'
#             'mpdecimal: for decimal'
#             'xz: for lzma'
#             'tk: for tkinter')
# provides=('python3')
# replaces=('python3')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
        '0001-python-config-Ignore-first-argument-if-it-s-a-.py-sc.patch'
        openssl-110f.patch
        dont-make-libpython-readonly.patch)
md5sums=(${PKG_CHECKSUM[$pkgname|md5sum]:-'2c68846471994897278364fc18730dd9'}
         '5c364c89632fd5080f6ec067d1df886e'
         '21254cd104b44dcc51c62af79c88f3bf'
         '60c9f7d02384f22834df06de5eb9c46a')

prepare() {
  cd Python-${pkgver}

  # GDB configure quirk
  patch -p1 -i ../0001-python-config-Ignore-first-argument-if-it-s-a-.py-sc.patch

  # https://bugs.python.org/issue30714
  patch -p1 -i ../openssl-110f.patch

  # FS#45809
  patch -p1 -i ../dont-make-libpython-readonly.patch

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # # rather than copies shipped in the tarball
  # rm -r Modules/expat
  # rm -r Modules/zlib
  # rm -r Modules/_ctypes/{darwin,libffi}*
  # rm -r Modules/_decimal/libmpdec
}

build() {
  cd Python-${pkgver}

  # Disable bundled pip & setuptools
  ./configure --prefix=${PREFIX} \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --without-ensurepip

  make
}

check() {
  cd Python-${pkgver}

  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
  LC_CTYPE=en_US.UTF-8 \
    "${srcdir}/Python-${pkgver}/python" -m test.regrtest -v -uall
}

package() {
  cd Python-${pkgver}

  # Hack to avoid building again
  sed -i 's/^all:.*$/all: build_all/' Makefile

  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" -depth -type d -name __pycache__ -exec rm -rf -- '{}' +
  find "${pkgdir}"${PREFIX}/lib/python${_pybasever} ! -type d \( -name "*.pyc" -o -name "*.pyo" -o -name "*.exe" \) -exec rm -f -- '{}' +

  rm -rf "${pkgdir}"${PREFIX}/lib/python${_pybasever}/test
  rm -rf "${pkgdir}"${PREFIX}/lib/python${_pybasever}/idlelib
  rm -rf "${pkgdir}"${PREFIX}/lib/python${_pybasever}/ensurepip
  rm -rf "${pkgdir}"${PREFIX}/lib/python${_pybasever}/distutils/tests

  # Why are these not done by default...
  ln -s python3               "${pkgdir}"${PREFIX}/bin/python
  ln -s python3-config        "${pkgdir}"${PREFIX}/bin/python-config
  # ln -s idle3                 "${pkgdir}"${PREFIX}/bin/idle
  # ln -s pydoc3                "${pkgdir}"${PREFIX}/bin/pydoc
  # ln -s python${_pybasever}.1 "${pkgdir}"${PREFIX}/share/man/man1/python.1

  # some useful "stuff" FS#46146
  install -dm755 "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"${PREFIX}/lib/python${_pybasever}/Tools/scripts/

  # License
  install -Dm644 LICENSE "${pkgdir}${PREFIX}/share/licenses/${pkgname}/LICENSE"
}
