# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from Arch Linux packaging project.
#
# Maintainer: Arthur Darcet <arthur.darcet at m4x.org>
# Contributor: Techlive Zheng <techlivezheng at gmail.com>

pkgname=libiconv
pkgver=${PKG_VERSION[$pkgname]:-1.15}
pkgrel=1
pkgdesc='Provides libiconv.so and libcharset.so'
arch=(i686 x86_64)
url='http://www.gnu.org/software/libiconv/'
license=('LGPL')
source=("http://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('ace8b5f2db42f7b3b3057585e80d9808')
options=('!staticlibs')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  sed '/LD_RUN_PATH/d' -i Makefile.in
  ./configure --prefix=${PREFIX} \
    --enable-shared \
    --disable-static \
    --enable-extra-encodings \
    --enable-relocatable \
    --disable-rpath \
    --enable-silent-rules \
    --enable-nls

  cp -f /usr/include/stdio.h srclib/stdio.in.h
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" LIBDIR="${PREFIX}/lib" install
  rm -f "${pkgdir}${PREFIX}"/lib/preloadable_libiconv.so
  mv "${pkgdir}${PREFIX}"/include/{iconv.h,libiconv.h}
  mv "${pkgdir}${PREFIX}"/bin/{iconv,libiconv}
  mv "${pkgdir}${PREFIX}"/share/man/man1/{,lib}iconv.1
  mv "${pkgdir}${PREFIX}"/share/man/man3/{,libiconv_}iconv.3
  mv "${pkgdir}${PREFIX}"/share/man/man3/{,libiconv_}iconvctl.3
  mv "${pkgdir}${PREFIX}"/share/man/man3/{,libiconv_}iconv_open.3
  mv "${pkgdir}${PREFIX}"/share/man/man3/{,libiconv_}iconv_close.3
  mv "${pkgdir}${PREFIX}"/share/man/man3/{,libiconv_}iconv_open_into.3
}
