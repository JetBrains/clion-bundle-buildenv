# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from Arch Linux packaging project.
#
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=cmake
pkgver=${PKG_VERSION[$pkgname]:-3.8.2}
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=(i686 x86_64)
url="http://www.cmake.org/"
license=('custom')
depends=(expat xz zlib)
# depends=('curl' 'libarchive' 'shared-mime-info' 'jsoncpp' 'libuv')
# makedepends=('qt5-base' 'python-sphinx' 'emacs' 'rhash')
# optdepends=('qt5-base: cmake-gui'
#             'libxkbcommon-x11: cmake-gui')
source=("https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('49ea6f74a81dfa8ed8336c94ed50e0a483b796132c260f4bef159f9c389d99cc7b26ecfcccdd8b0a8f5b52fddd277c17c547d2a2e8848da9912c464ada1cbe3f')
options=('!staticlibs')

build() {
  cd ${pkgname}-${pkgver}

  # Remove unwanted help files (takes too long to install them)
  rm -rf ./Help/*

  ./bootstrap --prefix=${PREFIX} \
    --system-expat         \
    --system-zlib          \
    --system-liblzma       \
    --no-system-bzip2      \
    --no-system-curl       \
    --no-system-jsoncpp    \
    --no-system-libarchive \
    --no-system-librhash   \
    --parallel=$(($(nproc)+1)) \
    -- \
    -DBUILD_TESTING=no \
    -DBUILD_CursesDialog=no \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_USE_OPENSSL=ON
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"${PREFIX}/share/${pkgname}-${pkgver%.*}/{completions,editors,Help,include,Templates}

  install -Dm644 Copyright.txt \
    "${pkgdir}"${PREFIX}/share/licenses/${pkgname}/LICENSE
}
