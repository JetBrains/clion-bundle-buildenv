# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=(aarch64 i686 x86_64)
license=('custom')
url="https://www.zlib.net/"
#depends=('glibc')
options=('!staticlibs')
source=("https://github.com/madler/zlib/archive/v${pkgver}.tar.gz")
sha256sums=('629380c90a77b964d896ed37163f5c3a34f6e6d897311f1df2a7016355c45eff')

prepare() {
	cd ${srcdir}/zlib-$pkgver
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd ${srcdir}/zlib-$pkgver
	CFLAGS="$CFLAGS -fPIC" ./configure --prefix=${PREFIX} \
		--shared

	make -j1 all
}

check() {
	cd ${srcdir}/zlib-$pkgver
	make test
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}${PREFIX}/share/licenses/zlib/LICENSE
}
