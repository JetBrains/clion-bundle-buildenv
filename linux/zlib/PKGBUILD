# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=zlib
epoch=1
pkgver=1.2.11
pkgrel=2
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=(i686 x86_64)
license=('custom')
url="https://www.zlib.net/"
#depends=('glibc')
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz")
md5sums=('1c9f62f0778697a09d36121ead88e08e')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
	cd ${srcdir}/zlib-$pkgver
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd ${srcdir}/zlib-$pkgver
	./configure \
		--prefix=${PREFIX} \
		--static

  make -j1 all
}

check() {
	cd ${srcdir}/zlib-$pkgver
	make test
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}${PREFIX}/share/licenses/zlib/LICENSE
}
