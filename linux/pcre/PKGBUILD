# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre
pkgver=8.41
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions'
arch=('i686' 'x86_64')
url='http://www.pcre.org/'
license=('BSD')
# depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'bash')
# validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("https://ftp.pcre.org/pub/pcre/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c160d22723b1670447341b08c58981c1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=${PREFIX} \
    --enable-unicode-properties \
    --enable-pcre16 \
    --enable-pcre32 \
    --enable-jit
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
