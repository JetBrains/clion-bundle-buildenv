# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre2
pkgver=10.43
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions'
arch=(aarch64 i686 x86_64)
url='http://www.pcre.org/'
license=('BSD')
# depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'bash')
# validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("https://github.com/PCRE2Project/pcre2/releases/download/pcre2-$pkgver/pcre2-$pkgver.tar.gz")
md5sums=('4a5cc02158cead5e926cdf1dd5e4304e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=${PREFIX} \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
