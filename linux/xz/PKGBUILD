# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=5.2.3
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=(i686 x86_64)
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
#depends=('sh')
source=("https://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('ef68674fb47a8b8e741b34e429d86e9d')
options=('!staticlibs')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=${PREFIX} \
		--enable-shared \
		--disable-static \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}${PREFIX}/share/licenses/xz/
	install -D -m644 COPYING* ${pkgdir}${PREFIX}/share/licenses/xz/
}
