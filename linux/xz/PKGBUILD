# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=5.4.5
pkgrel=2
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=(aarch64 i686 x86_64)
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
#depends=('sh')
source=("https://github.com/tukaani-project/xz/releases/download/v${pkgver}/xz-${pkgver}.tar.gz")
md5sums=('d2bb81e1a1a7808352c4ca28622f5c72')
options=('!staticlibs')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=${PREFIX} \
		--enable-shared \
		--disable-static \
		--disable-rpath
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}${PREFIX}/share/licenses/xz/
	install -D -m644 COPYING* ${pkgdir}${PREFIX}/share/licenses/xz/
}
