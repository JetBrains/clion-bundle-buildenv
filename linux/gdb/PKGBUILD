# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gdb
# gdb-common is a package that contains files common for all cross compiled versions
# of gdb (for arm/avr/...)
pkgname=gdb
pkgver=8.0
pkgrel=1
pkgdesc='The GNU Debugger'
arch=(i686 x86_64)
url='http://www.gnu.org/software/gdb/'
license=(GPL3)
makedepends=(libiconv expat xz zlib)
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
sha1sums=('148c8e783ebf9b281241d0566db59961191ec64d')
options=('!staticlibs' 'strip')

prepare() {
  cd gdb-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  ../gdb-${pkgver}/configure \
    --prefix=${PREFIX} \
    --enable-gdbserver=no \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --without-python \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make ${MAKEFLAGS}
}

package() {
  cd gdb-$pkgver
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
