# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=8.2
pkgrel=1
pkgdesc='The GNU Debugger'
arch=(i686 x86_64)
url='http://www.gnu.org/software/gdb/'
license=(GPL3)
depends=(libiconv expat xz zlib python)
options=('!staticlibs' 'strip')
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
md5sums=('b5a49dbff00d9a87fbe114d14b3101c0')

source "PKGBUILD.inc"

prepare() {
  cd gdb-$pkgver

  local s
  for s in "${source[@]}"; do
    if [[ "${s}" == *.patch  ]]; then
      msg2 "Applying patch %s" "${s}"
      patch -p1 -i ${srcdir}/"${s}"
    fi
  done

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  ../gdb-${pkgver}/configure \
    --with-pkgversion="GDB${PKGVERSION:+; ${PKGVERSION}}" \
    --prefix=${PREFIX} \
    --enable-targets=$(IFS=','; echo "${gdb_enable_targets[*]}") \
    --enable-gdbserver \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-rpath \
    --with-separate-debug-dir=/usr/lib/debug \
    --with-system-gdbinit=/etc/gdb/gdbinit \
    --with-python=${PREFIX}/bin/python-config \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd gdb-$pkgver
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
