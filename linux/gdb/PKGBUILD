# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from the Arch Linux packaging project.
#
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=8.0.1
pkgrel=1
pkgdesc='The GNU Debugger'
arch=(i686 x86_64)
url='http://www.gnu.org/software/gdb/'
license=(GPL3)
makedepends=(libiconv expat xz zlib)
depends=(python)
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
md5sums=('48cac527e6f3018b865ece021e9723ac')
options=('!staticlibs')

prepare() {
  cd gdb-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  local _enable_targets=(
    "i686-pc-mingw32"

    "i686-linux-gnu"
    "i686-w64-mingw32"
    "x86_64-linux-gnu"
    "x86_64-w64-mingw32"

    "aarch64-linux-gnu"
    "alpha-linux-gnu"
    "arm-linux-gnu"
    "arm-linux-gnueabi"
    "arm-linux-gnueabihf"
    "hppa-linux-gnu"
    "ia64-linux-gnu"
    "m68k-linux-gnu"
    "m68k-rtems"
    "mips-linux-gnu"
    "mipsel-linux-gnu"
    "mips64-linux-gnu"
    "mips64el-linux-gnu"
    "powerpc-linux-gnu"
    "powerpc-linux-gnuspe"
    "powerpc64le-linux-gnu"
    "powerpc64-linux-gnu"
    "s390-linux-gnu"
    "s390x-linux-gnu"
    "sh-linux-gnu"
    "sparc-linux-gnu"
    "sparc64-linux-gnu"
    "m32r-linux-gnu"
  )

  local _pkgversion_string="$(pkgversion_string)"

  ../gdb-${pkgver}/configure \
    --with-pkgversion="GDB${_pkgversion_string:+; ${_pkgversion_string}}" \
    --prefix=${PREFIX} \
    --enable-targets=$(IFS=','; echo "${_enable_targets[*]}") \
    --enable-gdbserver \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --with-python=${PREFIX}/bin/python-config \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd gdb-$pkgver
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
