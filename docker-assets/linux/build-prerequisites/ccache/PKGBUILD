# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ccache
pkgver=3.3.4
pkgrel=2
pkgdesc="A compiler cache"
arch=('i686' 'x86_64')
url="http://ccache.samba.org/"
license=('GPL3')
depends=('zlib')
source=("http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('95ab3c56284129cc2a32460c23069516')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -Dm 755 ccache ${pkgdir}/usr/bin/ccache
  install -Dm 644 ccache.1 ${pkgdir}/usr/share/man/man1/ccache.1

  install -d ${pkgdir}/usr/lib/ccache/bin

  local _prog
  for _prog in gcc g++ c++; do
    ln -s /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/$_prog
    ln -s /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-$_prog
  done
  for _prog in cc clang clang++; do
    ln -s /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/$_prog
  done
}
