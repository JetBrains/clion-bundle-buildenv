# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=gdb
pkgver=16.3
pkgrel=1
pkgdesc="GNU Debugger (mingw-w64)"
arch=(aarch64 i686 x86_64)
url="https://www.gnu.org/software/gdb/"
license=('GPL')
groups=("toolchain")
depends=("mingw-w64-runtime"
         "expat"
         "zlib"
         "xz"
         "python3-embed-prebuilt")
makedepends=("gmp" "mpfr" "libiconv")

options=('!staticlibs' 'strip')
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
md5sums=('f7a7e2d0a6d28622ac69a3623b23876b')
source "PKGBUILD.inc"

if [ "$CARCH" == aarch64 ]; then
  source+=(
    'aarch64.patch'
    'clang14.patch'
  )
  md5sums+=(
    'bfdfc9bc58123879a07fd923f2a709e4'
    'ffbd60e253aeae8e5d1f207b9958f084'
  )
fi

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  local s
  for s in "${source[@]}"; do
    if [[ "${s}" == *.patch  ]]; then
      msg2 "Applying patch %s" "${s}"
      patch -p1 -i ${srcdir}/"${s}"
    fi
  done

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}
  cd ${srcdir}/build-${CHOST}

  ../${pkgname}-${pkgver}/configure \
    --with-pkgversion="GDB${PKGVERSION:+; ${PKGVERSION}}" \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=${PREFIX} \
    --enable-targets=all \
    --$([ $CARCH == x86_64 ] && echo enable || echo disable)-gdbserver \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --with-python=${PREFIX}/bin/python-config.sh \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --with-{gmp,mpfr}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
