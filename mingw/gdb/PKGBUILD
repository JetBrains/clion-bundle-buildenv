# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=gdb
pkgver=8.1
pkgrel=1
pkgdesc="GNU Debugger (mingw-w64)"
arch=(i686 x86_64)
url="https://www.gnu.org/software/gdb/"
license=('GPL')
groups=("toolchain")
depends=("mingw-w64-runtime"
         "expat"
         "zlib"
         "xz"
         "python3-embed-prebuilt")
makedepends=("libiconv")

options=('!staticlibs' 'strip')
source=(https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz)
md5sums=('f46487561f9a16916a8102316f7fd105')

source "PKGBUILD.inc"

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  local s
  for s in "${source[@]}"; do
    if [[ "${s}" == *.patch  ]]; then
      msg2 "Applying patch %s" "${s}"
      patch -p1 -i ${srcdir}/"${s}"
    fi
  done

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}
  cd ${srcdir}/build-${CHOST}

  ../${pkgname}-${pkgver}/configure \
    --with-pkgversion="GDB${PKGVERSION:+; ${PKGVERSION}}" \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=${PREFIX} \
    --enable-targets=$(IFS=','; echo "${gdb_enable_targets[*]}") \
    --enable-gdbserver \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --with-python=${PREFIX}/bin/python-config.sh \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
