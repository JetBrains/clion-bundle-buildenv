pkgname=mpfr
pkgver=4.2.0
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=(i686 x86_64)
license=('LGPL')
options=('staticlibs')
depends=(gmp)
makedepends=(gmp)

source=(
  https://ftp.gnu.org/gnu/mpfr/mpfr-${pkgver}.tar.xz
  patches.patch
)

sha256sums=(
  06a378df13501248c1b2db5aa977a2c8126ae849a9d9b7be2546fb4a9c26d993
  811d3b45c8fc98affbeb0aba2d183ff17339ca2c31958b2f1dfeda1249cc3366
)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  apply_patches
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../${pkgname}-${pkgver}/configure \
    --host=${CHOST} \
    --target=${CHOST} \
    --build=${MACHTYPE} \
    --prefix=${PREFIX}/makedepends \
    --enable-static \
    --disable-shared \
    --with-gmp=${PREFIX}/makedepends

  make VERBOSE=1
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
  install_copying
}
