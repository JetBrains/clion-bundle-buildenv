pkgname=crt
pkgver=v11.0.0
pkgrel=1
pkgdesc="MinGW CRT"
arch=(i686 x86_64)
url='https://mingw-w64.sourceforge.io/'
license=('custom')
options=('!strip' 'staticlibs' '!buildflags' '!emptydirs')
depends=('headers')

source=(
  http://downloads.sourceforge.net/project/mingw-w64/mingw-w64/mingw-w64-release/mingw-w64-$pkgver.tar.bz2
  0001-Allow-to-use-bessel-and-complex-functions-without-un.patch
)

sha256sums=(
  bd0ea1633bd830204cc23a696889335e9d4a32b8619439ee17f22188695fcc5f
  d641257f7e1469aff89adc33e57702b75fe9667ca035978f890eae1020b6814c
)

prepare() {
  cd "${srcdir}/mingw-w64-${pkgver}"
  apply_patches
}

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../mingw-w64-${pkgver}/mingw-w64-crt/configure \
    --host=${CHOST} \
    --build=${MACHTYPE} \
    --prefix=${PREFIX}/${CHOST} \
    --enable-wildcard \
    --disable-dependency-tracking \
    $(
      [ "${CARCH}" == "x86_64" ] && {
        echo "--disable-lib32 --enable-lib64"
      } || {
        echo "--enable-lib32 --disable-lib64"
      }
    ) \
    --with-default-msvcrt=msvcrt \
    CFLAGS="-Wno-expansion-to-defined"

  make VERBOSE=1
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR=${pkgdir} install
  make DESTDIR=${PREFIX}/sysroot install
  install_copying
}
