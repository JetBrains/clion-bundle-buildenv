# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=libiconv
pkgver=1.17
pkgrel=1
arch=(aarch64 i686 x86_64)
url='https://www.gnu.org/software/libiconv/'
source=("https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        0002-fix-cr-for-awk-in-configure.all.patch
        fix-pointer-buf.patch)
sha256sums=('8f74213b56238c85a50a5329f77e06198771e70dd9a739779f4c02f65d971313'
            '89d5c0f666e50a0186cfb142ba7b77e8fe1ac4d65bdfd9ae14ae8d2f0045a87c'
            '7105be2643864f48022cc9ef5869637d1906c0edc2518f3e5eb6db12fcb71f1a')
options=('!libtool' 'staticlibs')
#makedepends=(gcc)

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0002-fix-cr-for-awk-in-configure.all.patch
  patch -p1 -i ${srcdir}/fix-pointer-buf.patch
}

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir build-${CHOST} && cd build-${CHOST}

  ../${pkgname}-${pkgver}/configure \
    --prefix=${PREFIX} \
    --host=${CHOST} \
    --target=${CHOST} \
    --docdir=${PREFIX}/share/doc/libiconv \
    --enable-static \
    --disable-shared \
    --enable-extra-encodings \
    --enable-relocatable \
    --disable-rpath \
    --enable-nls

  make
}

package() {
  pkgdesc='Character encoding conversion library (mingw-w64)'
  license=(LGPL2 documentation:'GPL3') # This is LGPL except for documentation, see README

  cd "${srcdir}/build-${CHOST}"
  make install DESTDIR="${pkgdir}"

  rm -f  "${pkgdir}${PREFIX}"/lib/charset.alias
  rm -fr "${pkgdir}${PREFIX}"/share/locale
  rm -f  "${pkgdir}${PREFIX}"/share/doc/libiconv/*.1.*
  rm -fr "${pkgdir}${PREFIX}"/share/man/man1

  # Licenses
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/README"                 "${pkgdir}${PREFIX}/share/licenses/${pkgname}/README"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING"                "${pkgdir}${PREFIX}/share/licenses/${pkgname}/COPYING"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING.LIB"            "${pkgdir}${PREFIX}/share/licenses/${pkgname}/COPYING.LIB"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/libcharset/COPYING.LIB" "${pkgdir}${PREFIX}/share/licenses/${pkgname}/libcharset/COPYING.LIB"
}
