# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=libiconv
pkgver=1.17
pkgrel=1
arch=(i686 x86_64)
url='https://www.gnu.org/software/libiconv/'
license=(LGPL2 documentation:'GPL3') # This is LGPL except for documentation, see README
options=('!libtool' 'staticlibs')

source=(
  https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
  0002-fix-cr-for-awk-in-configure.all.patch
  0003-add-cp65001-as-utf8-alias.patch
  0004-fix-makefile-devel-assuming-gcc.patch
  fix-pointer-buf.patch
)

sha256sums=(
  8f74213b56238c85a50a5329f77e06198771e70dd9a739779f4c02f65d971313
  89d5c0f666e50a0186cfb142ba7b77e8fe1ac4d65bdfd9ae14ae8d2f0045a87c
  cb2b1cca44c9b9bd11c3bd33997c10d22dff263542275324c88f34b59910ef87
  2f08ca8681e4c85cdb2e6835921a502fd623dee4bf8ca10aa3c66747ede58a47
  6b6e2393840f4dc6067587165777b1a07978f4c05247d7c1010a45ad251bbeeb
)

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  apply_patches
  make -f Makefile.devel all
}

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir build-${CHOST} && cd build-${CHOST}

  LDFLAGS="-lssp" ../${pkgname}-${pkgver}/configure \
    --host=${CHOST} \
    --target=${CHOST} \
    --build=${MACHTYPE} \
    --prefix=${PREFIX}/${CHOST} \
    --enable-static \
    --disable-shared \
    --enable-extra-encodings \
    --enable-relocatable \
    --disable-rpath \
    --enable-nls

  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make -C lib        install DESTDIR="${pkgdir}"
  make -C libcharset install DESTDIR="${pkgdir}"

  rm -f  "${pkgdir}${PREFIX}"/lib/charset.alias
  rm -fr "${pkgdir}${PREFIX}"/share/locale
  rm -f  "${pkgdir}${PREFIX}"/share/doc/libiconv/*.1.*
  rm -fr "${pkgdir}${PREFIX}"/share/man/man1

  # Licenses
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/README"                 "${pkgdir}${PREFIX}/licenses/${pkgname}/README"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING"                "${pkgdir}${PREFIX}/licenses/${pkgname}/COPYING"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING.LIB"            "${pkgdir}${PREFIX}/licenses/${pkgname}/COPYING.LIB"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/libcharset/COPYING.LIB" "${pkgdir}${PREFIX}/licenses/${pkgname}/libcharset/COPYING.LIB"
}
