# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=libiconv
pkgver=1.16
pkgrel=1
arch=(i686 x86_64)
url='https://www.gnu.org/software/libiconv/'
source=("https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        0001-compile-relocatable-in-gnulib.mingw.patch
        0002-fix-cr-for-awk-in-configure.all.patch
        fix-pointer-buf.patch)
sha256sums=('e6a1b1b589654277ee790cce3734f07876ac4ccfaecbee8afa0b649cf529cc04'
            '7e905d21d670672b8a6a3bd80e5b8244b38bb7021a15fc7bdd0229125a3a68ee'
            'fe1a82f0836d65f895bebe9307bbe70ae53e5c872dd727f2042c46edc7cf060d'
            'fc1921f9d8ae21be0d1ee8a037fbb89aa066865f193a4883aa6e673827abba88')
options=('!libtool' 'staticlibs')
#makedepends=(gcc)

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-compile-relocatable-in-gnulib.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-cr-for-awk-in-configure.all.patch
  patch -p1 -i ${srcdir}/fix-pointer-buf.patch
}

build() {
  [[ -d build-${CHOST} ]] && rm -rf build-${CHOST}
  mkdir build-${CHOST} && cd build-${CHOST}

  ../${pkgname}-${pkgver}/configure \
    --prefix=${PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --docdir=${PREFIX}/share/doc/libiconv \
    --enable-static \
    --disable-shared \
    --enable-extra-encodings \
    --enable-relocatable \
    --disable-rpath \
    --enable-nls

  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check
}

package() {
  pkgdesc='Character encoding conversion library (mingw-w64)'
  license=(LGPL2 documentation:'GPL3') # This is LGPL except for documentation, see README

  cd "${srcdir}/build-${CHOST}"
  make -C lib        install DESTDIR="${pkgdir}"
  make -C libcharset install DESTDIR="${pkgdir}"

  rm -f  "${pkgdir}${PREFIX}"/lib/charset.alias
  rm -fr "${pkgdir}${PREFIX}"/share/locale
  rm -f  "${pkgdir}${PREFIX}"/share/doc/libiconv/*.1.*
  rm -fr "${pkgdir}${PREFIX}"/share/man/man1

  # Licenses
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/README"                 "${pkgdir}${PREFIX}/share/licenses/${pkgname}/README"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING"                "${pkgdir}${PREFIX}/share/licenses/${pkgname}/COPYING"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING.LIB"            "${pkgdir}${PREFIX}/share/licenses/${pkgname}/COPYING.LIB"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/libcharset/COPYING.LIB" "${pkgdir}${PREFIX}/share/licenses/${pkgname}/libcharset/COPYING.LIB"
}
