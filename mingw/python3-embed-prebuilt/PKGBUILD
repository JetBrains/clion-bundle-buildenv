# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>

_realname=python3
pkgname=${_realname}-embed
pkgver=3.11.0
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="A high-level scripting language (prebuilt; win32)"
arch=(aarch64 i686 x86_64)
license=('PSF')
url="https://www.python.org/"
options=('!staticlibs' '!strip')

if [ "${CARCH}" == aarch64 ]; then
  _archsuffix=arm64; _embed_zip_md5='e3dbbd5d63c6cb203adc6c0c8ca5f5f7'
elif [ "${CARCH}" == x86_64 ]; then
  _archsuffix=amd64; _embed_zip_md5='7df0f4244e5a66760b7caaed58e86c93'
else
  _archsuffix=win32; _embed_zip_md5='0888959642cc8af087d88da3866490a5'
fi

source=(https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz
        https://www.python.org/ftp/python/${pkgver}/python-${pkgver}-embed-${_archsuffix}.zip
        'python-config.sh.in')
noextract=(python-${pkgver}-embed-${_archsuffix}.zip)
md5sums=('fe92acfa0db9b9f5044958edb451d463'
         "${_embed_zip_md5}"
         'a83b453497b1c994271cdcdd91641ef3')

prepare() {
  cat python-config.sh.in | sed -e "
    s|@prefix@|${PREFIX//|/\\|}|g
    s|@CFLAGS@|${CFLAGS//|/\\|}|g
    s|@VERSION@|${_pybasever}|g
  " > python-config.sh

  cd Python-${pkgver}
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}

  (cd ${srcdir}/build-${CHOST}; bsdtar -xf ${srcdir}/python-${pkgver}-embed-${_archsuffix}.zip)
}

package() {
  cd ${srcdir}/build-${CHOST}

  install -dm755 ${pkgdir}${PREFIX}/include/python${_pybasever}
  install -dm755 ${pkgdir}${PREFIX}/bin/python${_pybasever}
  install -dm755 ${pkgdir}${PREFIX}/lib/python${_pybasever}

  cp -r ${srcdir}/Python-${pkgver}/Include/* ${pkgdir}${PREFIX}/include/python${_pybasever}

  install -Dm644 -t ${pkgdir}${PREFIX}/include/python${_pybasever} ${srcdir}/Python-${pkgver}/PC/pyconfig.h
  install -Dm644 -t ${pkgdir}${PREFIX}/bin/ python${_pybasever/./}.zip *._pth
  install -Dm755 -t ${pkgdir}${PREFIX}/bin/ *.{dll,pyd,exe} ${srcdir}/python-config.sh

  install -dm755 ${pkgdir}${PREFIX}/lib/python${_pybasever}/config
  ln -sT ../../../bin/python${_pybasever/./}.dll ${pkgdir}${PREFIX}/lib/python${_pybasever}/config/python${_pybasever}.dll.a
}
