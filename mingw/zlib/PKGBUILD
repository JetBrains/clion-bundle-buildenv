# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=zlib
pkgver=1.2.13
pkgrel=1
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP (mingw-w64)"
arch=(i686 x86_64)
license=(ZLIB)
url="http://www.zlib.net/"
options=('staticlibs')

source=(
  # https://github.com/madler/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.zip
  https://github.com/madler/zlib/releases/download/v1.2.13/zlib1213.zip
  01-zlib-1.2.11-1-buildsys.mingw.patch
  03-dont-put-sodir-into-L.mingw.patch
  04-fix-largefile-support.patch
)

sha256sums=(
  SKIP
  8175e70c980ffc48a2e90c6f0431ddb80ef6ff690f9e8906dd6a756238552fa5
  7287d12db57dcf0f66964c861100bf06ebe1ddcb243e7ee0773fcab1b2935596
  4d8ebda55b95d19cd7c467c486d4c42d2bad6db9ff5dca361f6d8c24120e5f30
)

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h >LICENSE
  apply_patches
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  CC=$CHOST-gcc \
  AR=$CHOST-ar \
  RANLIB=$CHOST-ranlib \
  ./configure \
    --prefix=${PREFIX}/${CHOST} \
    --static

  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}"

  install -Dm644 LICENSE ${pkgdir}${PREFIX}/licenses/${pkgname}/LICENSE
}
