# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=zlib
pkgver=1.2.11
pkgrel=1
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP (mingw-w64)"
arch=(i686 x86_64)
license=(ZLIB)
url="http://www.zlib.net/"
#depends=("bzip2")
#makedepends=('git')
options=('staticlibs')

source=("http://zlib.net/current/${pkgname}-${pkgver}.tar.gz"
        '01-zlib-1.2.11-1-buildsys.mingw.patch'
        '03-dont-put-sodir-into-L.mingw.patch'
        '04-fix-largefile-support.patch')

md5sums=('1c9f62f0778697a09d36121ead88e08e'
         '508d3a2d258cd808a6f2138256ddfef8'
         '44e20c3e1445075df929a5501449f329'
         '78c2f67bb20e6b2f491a5c4060d18257')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h >LICENSE
  apply_patches
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  CC=$CHOST-gcc \
  AR=$CHOST-ar \
  RANLIB=$CHOST-ranlib \
  ./configure \
    --prefix=${PREFIX}/${CHOST} \
    --static

  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}"

  install -Dm644 LICENSE ${pkgdir}${PREFIX}/licenses/${pkgname}/LICENSE
}
