# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=expat
pkgver=2.2.1
pkgrel=1
pkgdesc="An XML parser library (mingw-w64)"
arch=(i686 x86_64)
url="https://libexpat.github.io/"
license=(MIT)
options=('strip' 'staticlibs')

source=(
  "https://github.com/libexpat/libexpat/releases/download/R_$(echo $pkgver | tr . _)/$pkgname-$pkgver.tar.bz2"
  '001-fix-extension.patch'
  '002-fix-tests.patch'
)

md5sums=(
  'd9c3baeab58774cefc2f04faf29f2cf8'
  'f0896e172059ce210ce70d2a37f4a04e'
  '64340503ab966d5059db5ae564b7b6ec'
)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  apply_patches
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../${pkgname}-${pkgver}/configure \
    --prefix=${PREFIX} \
    --host=${CHOST} \
    --target=${CHOST} \
    --disable-shared \
    --enable-static

  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
  install_copying
}
