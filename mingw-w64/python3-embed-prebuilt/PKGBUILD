# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>

_realname=python3
pkgname=${_realname}-embed
pkgver=3.6.3
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="A high-level scripting language (prebuilt; win32)"
arch=(i686 x86_64)
license=('PSF')
url="https://www.python.org/"
options=('!staticlibs' 'strip')
if [ "${CARCH}" == x86_64 ]
  then _archsuffix=amd64; _embed_zip_md5='b1daa2a41589d7504117991104b96fe5'
  else _archsuffix=win32; _embed_zip_md5='cf1c75ad7ccf9dec57ba7269198fd56b'
fi
source=(https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz
        https://www.python.org/ftp/python/${pkgver}/python-${pkgver}-embed-${_archsuffix}.zip
        'python-config.sh.in'
        '0001-pyconfig-Disable-definition-of-hypot-as-_hypot.patch')
noextract=(python-${pkgver}-embed-${_archsuffix}.zip)
md5sums=('b9c2c36c33fb89bda1fefd37ad5af9be'
         "${_embed_zip_md5}"
         '1809b9ab4d12c472754d99d8aa235d6a'
         'ab052c29a0d73198bc4f81f9ac32f8c1')

prepare() {
  cat python-config.sh.in | sed -e "
    s|@prefix@|${PREFIX//|/\\|}|g
    s|@CFLAGS@|${CFLAGS//|/\\|}|g
    s|@VERSION@|${_pybasever}|g
  " > python-config.sh

  cd Python-${pkgver}

  patch -p1 -i ${srcdir}/0001-pyconfig-Disable-definition-of-hypot-as-_hypot.patch
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}

  (cd ${srcdir}/build-${CHOST}; bsdtar -xf ${srcdir}/python-${pkgver}-embed-${_archsuffix}.zip)
}

package() {
  cd ${srcdir}/build-${CHOST}

  install -Dm644 -t ${pkgdir}${PREFIX}/include/python${_pybasever} ${srcdir}/Python-${pkgver}/Include/*
  install -Dm644 -t ${pkgdir}${PREFIX}/include/python${_pybasever} ${srcdir}/Python-${pkgver}/PC/pyconfig.h
  install -Dm644 -t ${pkgdir}${PREFIX}/bin/ python${_pybasever/./}.zip *._pth
  install -Dm755 -t ${pkgdir}${PREFIX}/bin/ *.{dll,pyd} ${srcdir}/python-config.sh

  install -dm755 ${pkgdir}${PREFIX}/lib/python${_pybasever}/config
  ln -sT ../../../bin/python${_pybasever/./}.dll ${pkgdir}${PREFIX}/lib/python${_pybasever}/config/python${_pybasever}.dll.a
}
