# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=gdb
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=8.0
pkgrel=2
pkgdesc="GNU Debugger (mingw-w64)"
arch=(i686 x86_64)
url="https://www.gnu.org/software/gdb/"
license=('GPL')
groups=("toolchain")
depends=("expat"
         "libiconv"
         #"python2"
         "python2-prebuilt-win32"
         "zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("libiconv"
#             "ncurses"
             "xz")
options=('!staticlibs' 'strip')
source=(https://ftp.gnu.org/gnu/gdb/${_realname}-${pkgver}.tar.xz{,.sig}
        'gdb-fix-display-tabs-on-mingw.patch'
        #'gdb-mingw-gcc-4.7.patch'
        'gdb-7.8.1-mingw-gcc-4.7.patch'
        'gdb-7.6.2-mingw-gcc-4.7.patch'
        'gdb-perfomance.patch'
        'gdb-fix-using-gnu-print.patch'
        'gdb-7.12-dynamic-libs.patch')
sha256sums=('f6a24ffe4917e67014ef9273eb8b547cb96a13e5ca74895b06d683b391f3f4ee'
            'SKIP'
            'a3e64bf3acc8e92016d493aa1e2fbb5b55c67bfce4359b3578d7a6b5e5d5692a'
            '225608a1e486ee98641611804b07209915367c6767a735ae928175ad45b93060'
            '82ea717b628c8970055a9cbb8edaed8fbebc2cac27467337426dfc4b4c514a6b'
            '6be0f95483e02d5447c93ab5e2c9554d254c6888b1fa3f3a4c011cd6a0a403ad'
            '264eba33ef71c4762514a634dcf94fdb778914dbba6ce99e63804fe063225d97'
            '4398bd83a798baa15c0f91878391a0882239a682cf523ef6551766cba7b03699')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  # https://sourceware.org/ml/gdb-patches/2013-11/msg00224.html
  #patch -p1 -i ${srcdir}/gdb-fix-display-tabs-on-mingw.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15559
  # patch -p1 -i ${srcdir}/gdb-mingw-gcc-4.7.patch
  # patch -p1 -i ${srcdir}/gdb-${pkgver}-mingw-gcc-4.7.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15412
  patch -p1 -i ${srcdir}/gdb-perfomance.patch

  patch -p1 -i ${srcdir}/gdb-fix-using-gnu-print.patch

  # https://sourceware.org/bugzilla/show_bug.cgi?id=21078
  patch -p1 -i ${srcdir}/gdb-7.12-dynamic-libs.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}
  cd ${srcdir}/build-${CHOST}

  local _enable_targets=(
    "i686-pc-mingw32"

    "i686-linux-gnu"
    "i686-w64-mingw32"
    "x86_64-linux-gnu"
    "x86_64-w64-mingw32"

    "aarch64-linux-gnu"
    "alpha-linux-gnu"
    "arm-linux-gnu"
    "arm-linux-gnueabi"
    "arm-linux-gnueabihf"
    "hppa-linux-gnu"
    "ia64-linux-gnu"
    "m68k-linux-gnu"
    "m68k-rtems"
    "mips-linux-gnu"
    "mipsel-linux-gnu"
    "mips64-linux-gnu"
    "mips64el-linux-gnu"
    "powerpc-linux-gnu"
    "powerpc-linux-gnuspe"
    "powerpc64le-linux-gnu"
    "powerpc64-linux-gnu"
    "s390-linux-gnu"
    "s390x-linux-gnu"
    "sh-linux-gnu"
    "sparc-linux-gnu"
    "sparc64-linux-gnu"
    "m32r-linux-gnu"
  )

  ../${_realname}-${pkgver}/configure \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=${PREFIX} \
    --enable-targets=$(IFS=','; echo "${_enable_targets[*]}") \
    --enable-gdbserver=no \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --with-python=${PREFIX}/bin/python-config-u.sh \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
