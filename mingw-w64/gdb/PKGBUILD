# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=gdb
pkgver=8.0.1
pkgrel=4
pkgdesc="GNU Debugger (mingw-w64)"
arch=(i686 x86_64)
url="https://www.gnu.org/software/gdb/"
license=('GPL')
groups=("toolchain")
depends=("expat"
         "libiconv"
         "zlib"
         "xz"
         #"python2"
         "python3-embed-prebuilt")
checkdepends=('dejagnu' 'bc')
#makedepends=("libiconv"
#             "ncurses"
#             "xz")
options=('!staticlibs' 'strip')
source=(https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz
        'gdb-fix-display-tabs-on-mingw.patch'
        #'gdb-mingw-gcc-4.7.patch'
        'gdb-7.8.1-mingw-gcc-4.7.patch'
        'gdb-7.6.2-mingw-gcc-4.7.patch'
        'gdb-perfomance.patch'
        'gdb-fix-using-gnu-print.patch'
        'gdb-7.12-dynamic-libs.patch'
        'CPP-10055-py3k-fix-python_run_simple_file-using-deprecated-api.patch'
        'CPP-10461-gdb-limit-cp_print_value_fields-recursion.patch'
        'CPP-7331-MI-Allow-non-raw-varobj-evaluation.patch'
        'CPP-11966-Fix-type-of-values-representing-optimized-out-static-members.patch')
md5sums=('48cac527e6f3018b865ece021e9723ac'
         'e48292d553379a9765c82e80e618ffd2'
         '8cc7f920caf4dddfc0d5d3fcd347ebb4'
         '7cc5df72d958ef3e4b1ffa9f7b069e6f'
         'e9b7c252fedfa950998f5f3b32536461'
         'd7112b220466a294aa266bb4d5c06a59'
         'aa9da2a8653357d1849f3a1092110461'
         'c90fe93130e828fb22fbc3aded9605f0'
         '18f5d49270a9ac18af16c53ccfa302a2'
         '4b577626e1716dedf85dccae46376d07'
         '0f80599eaa6d0a9495ce754a1999f6db')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # https://sourceware.org/ml/gdb-patches/2013-11/msg00224.html
  #patch -p1 -i ${srcdir}/gdb-fix-display-tabs-on-mingw.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15559
  # patch -p1 -i ${srcdir}/gdb-mingw-gcc-4.7.patch
  # patch -p1 -i ${srcdir}/gdb-${pkgver}-mingw-gcc-4.7.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15412
  patch -p1 -i ${srcdir}/gdb-perfomance.patch

  patch -p1 -i ${srcdir}/gdb-fix-using-gnu-print.patch

  # https://sourceware.org/bugzilla/show_bug.cgi?id=21078
  patch -p1 -i ${srcdir}/gdb-7.12-dynamic-libs.patch

  # https://sourceware.org/bugzilla/show_bug.cgi?id=15600
  patch -p1 -i ${srcdir}/CPP-10055-py3k-fix-python_run_simple_file-using-deprecated-api.patch

  # Bugzilla #13669: https://sourceware.org/bugzilla/attachment.cgi?id=8993&action=diff
  patch -p1 -i ${srcdir}/CPP-10461-gdb-limit-cp_print_value_fields-recursion.patch

  # https://sourceware.org/ml/gdb-patches/2018-02/msg00030.html
  patch -p1 -i ${srcdir}/CPP-7331-MI-Allow-non-raw-varobj-evaluation.patch

  # https://sourceware.org/ml/gdb-patches/2018-02/msg00077.html
  patch -p1 -i ${srcdir}/CPP-11966-Fix-type-of-values-representing-optimized-out-static-members.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}
  cd ${srcdir}/build-${CHOST}

  local _enable_targets=(
    "i686-pc-mingw32"

    "i686-linux-gnu"
    "i686-w64-mingw32"
    "x86_64-linux-gnu"
    "x86_64-w64-mingw32"

    "aarch64-linux-gnu"
    "alpha-linux-gnu"
    "arm-linux-gnu"
    "arm-linux-gnueabi"
    "arm-linux-gnueabihf"
    "hppa-linux-gnu"
    "ia64-linux-gnu"
    "m68k-linux-gnu"
    "m68k-rtems"
    "mips-linux-gnu"
    "mipsel-linux-gnu"
    "mips64-linux-gnu"
    "mips64el-linux-gnu"
    "powerpc-linux-gnu"
    "powerpc-linux-gnuspe"
    "powerpc64le-linux-gnu"
    "powerpc64-linux-gnu"
    "s390-linux-gnu"
    "s390x-linux-gnu"
    "sh-linux-gnu"
    "sparc-linux-gnu"
    "sparc64-linux-gnu"
    "m32r-linux-gnu"
  )

  ../${pkgname}-${pkgver}/configure \
    --with-pkgversion="GDB${PKGVERSION:+; ${PKGVERSION}}" \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=${PREFIX} \
    --enable-targets=$(IFS=','; echo "${_enable_targets[*]}") \
    --enable-gdbserver \
    --enable-64-bit-bfd \
    --disable-sim \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${PREFIX}/etc/gdbinit \
    --with-python=${PREFIX}/bin/python-config.sh \
    --without-guile \
    --with-lib{expat,iconv,zlib,lzma}=${PREFIX} \
    --disable-tui

  make
}

package() {
  cd ${srcdir}/build-${CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${PREFIX}/share/{man,info}

  rm -f ${pkgdir}${PREFIX}/include/*.h
  rm -f ${pkgdir}${PREFIX}/lib/*.a
}
