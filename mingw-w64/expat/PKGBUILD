# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>
#
# This work is derived from MinGW-W64 packaging project.
#
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=expat
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=2.2.1
pkgrel=2
pkgdesc="An XML parser library (mingw-w64)"
arch=(i686 x86_64)
url="https://libexpat.github.io/"
license=(MIT)
#makedepends=("gcc" "pkg-config")
options=('strip' 'staticlibs')
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        001-fix-extension.patch
        002-fix-tests.patch)
sha256sums=('1868cadae4c82a018e361e2b2091de103cd820aaacb0d6cfa49bd2cd83978885'
            '3d97bc769ad70d81ba9c488619e71cf21f43122076857a0b7d0ed425a1cbe68e'
            'abcf34b90ed801cc2810f6d333bca2997aadfe95dbedd1509bd54c14ecca1042')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-extension.patch
  patch -p1 -i ${srcdir}/002-fix-tests.patch
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${CHOST}" ]] && rm -rf "${srcdir}/build-${CHOST}"
  mkdir -p "${srcdir}/build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${PREFIX} \
    --host=${CHOST} \
    --target=${CHOST} \
    --disable-shared \
    --enable-static

  make
}

check() {
  cd "${srcdir}/build-${CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${PREFIX}/share/licenses/${_realname}/COPYING"
}
