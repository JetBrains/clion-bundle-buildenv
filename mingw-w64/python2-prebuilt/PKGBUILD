# Eldar Abusalimov <Eldar.Abusalimov@jetbrains.com>

_realname=python2
pkgname=${_realname}-prebuilt
pkgver=${PKG_VERSION[$pkgname]:-2.7.13}
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="A high-level scripting language (prebuilt; win32)"
arch=(i686 x86_64)
license=('PSF')
url="https://www.python.org/"
#makedepends=('wine')
options=('!staticlibs' 'strip')
source=(https://www.python.org/ftp/python/${pkgver}/python-${pkgver}$([ "${CARCH}" == x86_64 ] && echo .amd64).msi
        'python-config.sh'
        'python-config-u.sh'
        '0001-pyconfig-Disable-definition-of-hypot-as-_hypot.patch')
md5sums=('SKIP'
         'ddead55433eea001c724fba5a6246f0a'
         '8f753e446e447e43ba1fae3ce3b8fd83'
         'f24472281e9dd6161a69cfd436f0f848')

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir ${srcdir}/build-${CHOST}

  msiexec /a python-${pkgver}*.msi /qb TARGETDIR=${srcdir}/build-${CHOST}
}

package() {
  cd ${srcdir}/build-${CHOST}

  patch -p1 -i ${srcdir}/0001-pyconfig-Disable-definition-of-hypot-as-_hypot.patch

  mkdir -p ${pkgdir}${PREFIX}/include
  cp -rT include ${pkgdir}${PREFIX}/include/python${_pybasever}

  mkdir -p ${pkgdir}${PREFIX}/bin
  cp -T python${_pybasever/./}.dll ${pkgdir}${PREFIX}/bin/python${_pybasever}.dll
  cp ${srcdir}/python-config*.sh ${pkgdir}${PREFIX}/bin/
  chmod a+x ${pkgdir}${PREFIX}/bin/*

  mkdir -p ${pkgdir}${PREFIX}/lib
  cp -rT Lib ${pkgdir}${PREFIX}/lib/python${_pybasever}
  mkdir -p ${pkgdir}${PREFIX}/lib/python${_pybasever}/config
  cp libs/libpython${_pybasever/./}.a ${pkgdir}${PREFIX}/lib/python${_pybasever}/config
  ln -sT ../../../bin/python${_pybasever}.dll ${pkgdir}${PREFIX}/lib/python${_pybasever}/config/python${_pybasever}.dll.a
}
